#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Tester1"]
#Text["Tester2"]
#include"script/default_system/Default_ShotConst.txt"
#include"script/default_system/Default_Effect.txt"
let objBoss;
let objScene = GetEnemyBossSceneObjectID();
@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(500);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(60);
	}
	case(EV_REQUEST_SPELL_SCORE)
	{
		SetScriptResult(30000);
	}
}
@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	moveBoss(GetCenterX(), 60, 60);

	ObjEnemyBossScene_StartSpell(objScene);

	TDrawLoop;
	TFinalize;
	movement;
	MainTask;
}
@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, bossX(), bossY(), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, bossX(), bossY(), 24);
	yield;
}
function GetCenterX(){
	return GetStgFrameWidth() / 2;
}
function GetCenterY(){
	return GetStgFrameHeight() / 2;
}
function wait(n) {
	loop(n) {
		yield;
	}
}
function bossAlive() {
	if (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		return true;
	} else {
		return false;
	}
}
function bossX() {
	return ObjMove_GetX(objBoss);
}
function bossY() {
	return ObjMove_GetY(objBoss);
}
function moveBoss(x, y, z) {
	ObjMove_SetDestAtFrame(objBoss, x, y, z);
}
function checkBomb() {
	return ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT);
}
function checkDeath() {
	return ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT)
}
task MainTask {
	while(bossAlive()) {
		let angleT = GetAngleToPlayer(objBoss);
		CreateShotA1(bossX(), bossY(), 2, angleT, DS_BALL_S_RED, 5);
		wait(30);
	}
}
task TDrawLoop {
	let imgExRumia = GetCurrentScriptDirectory() ~ "../ExRumia/ExRumia.png";
	ObjPrim_SetTexture(objBoss, imgExRumia);
	ObjSprite2D_SetSourceRect(objBoss, 64, 1, 127, 64);
	ObjSprite2D_SetDestCenter(objBoss);
}
task TFinalize {
	while(bossAlive()) {
		yield;
	}
	if (checkBomb() + checkDeath() == 0) {
		AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
	}
	Obj_Delete(objBoss);
	DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
	SetAutoDeleteObject(true);
	CloseScript(GetOwnScriptID());
	return;
}
task movement {
	while (bossAlive()) {
		moveBoss(rand(GetCenterX() + 90, GetCenterX() - 90), rand(GetCenterY - 60, GetCenterY - 120), 60);
		wait(240);
	}
}